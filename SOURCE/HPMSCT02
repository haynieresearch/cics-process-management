      **************************************************************************
      * CATEGORY.: HPMS CICS PROGRAMS
      * GROUP....: COBOL
      * AUTHOR...: LANCE HAYNIE <LANCE@HAYNIEMAIL.COM>
      * DATE.....: 2017-09-29
      * PURPOSE..: EDIT/VIEW TEST LOG RECORD
      **************************************************************************
      * MODIFICATIONS
      * 2017-09-29 - LHAYNIE - INITIAL VERSION
      * 2017-10-12 - LHAYNIE - ADDED START ON LAST RECORD
      * 2017-10-12 - LHAYNIE - ADDED NEXT/PREV RECORD FUNCTIONS
      **************************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID.  HPMSCT02.
      **************************************************************************
       DATA DIVISION.
       WORKING-STORAGE SECTION.
       01  LOCAL-TRAIN-ID               PIC X(4) VALUE 'TL02'.
       01  MASTER-RECORD-LRECL          PIC S9(4) COMP VALUE 1270.
       01  DATA-RESP-CODE               PIC S9(8) COMP.
       01  XCTL-RESP-CODE               PIC S9(8) COMP.
       01  WS-RESPONSE                  PIC S9(8) COMP.
       01  WS-REASON-CODE               PIC S9(8) COMP.
       01  X-EIB-MESSAGE.
           05  FILLER                   PIC X(8) VALUE 'EIBRESP='.
           05  X-EIB-RESP               PIC 9(10) VALUE 0.
           05  FILLER                   PIC X(11) VALUE ', EIBRESP2='.
           05  X-EIB-RESP2              PIC 9(10) VALUE 0.
           05  FILLER                   PIC X VALUE SPACE.
           05  X-EIB-USER-MSG           PIC X(38) VALUE SPACES.
       01  WS-COMMAREA.
           05  CA-TLOG-RECORD.
               10  CA-RECORD-ID         PIC X(6).
               10  CA-RECORD-ID-N REDEFINES CA-RECORD-ID PIC 9(6).
               10  FILLER               PIC X(1264).
       COPY HPMSB02.
       COPY TLOG.
       COPY DFHAID.
       COPY DFHBMSCA.
      **************************************************************************
       LINKAGE SECTION.
       01  DFHCOMMAREA.
           05  LS-TLOG-RECORD.
               10  LS-RECORD-ID         PIC X(6).
               10  FILLER               PIC X(1264).
      **************************************************************************
       PROCEDURE DIVISION.
           IF EIBCALEN > ZERO
               MOVE DFHCOMMAREA TO WS-COMMAREA
           END-IF.

           PERFORM CLEAR-MESSAGES.

           EXEC CICS HANDLE CONDITION
                ERROR   (GENERAL-ERROR-ROUTINE)
                MAPFAIL (MAP-FAIL-ROUTINE)
                NOTFND  (GENERAL-ERROR-ROUTINE)
           END-EXEC.

           EVALUATE TRUE
             WHEN EIBCALEN = ZERO
                  PERFORM DATA-INITIALIZE-SCREEN-RECORD
                  PERFORM GET-LAST-RECORD
             WHEN EIBAID = DFHCLEAR
                  PERFORM DATA-INITIALIZE-SCREEN-RECORD
                  PERFORM SCREEN-IO-SEND-UPDATE
             WHEN EIBAID = DFHPF3
                  PERFORM END-OF-PROGRAM
             WHEN EIBAID = DFHPF5
                  PERFORM UPDATE-INFORMATION
             WHEN EIBAID = DFHPF7
                  PERFORM DATA-INITIALIZE-SCREEN-RECORD
                  PERFORM GET-PREV-RECORD
             WHEN EIBAID = DFHPF8
                  PERFORM DATA-INITIALIZE-SCREEN-RECORD
                  PERFORM GET-NEXT-RECORD
             WHEN EIBAID = DFHENTER
                  PERFORM GET-INFORMATION
             WHEN OTHER
                  MOVE "YOU MUST ENTER A RECORD TO LOOKUP
      -         " TO MSGO
                  PERFORM RECOVERY-ROUTINE
             END-EVALUATE.
             PERFORM END-OF-PROGRAM.
      **************************************************************************
       BUILD-HEADER.
           MOVE 'TEST AND EXPERIMENT LOG' TO TITLE1O.
           MOVE 'INQUIRY/UPDATE' TO TITLE2O.
           MOVE 'HPMSCT02' TO PNAMEO.
           MOVE 'ENTER=LOOKUP PFKEYS: PF3=EXIT PF5=UPDATE PF7=PREV RECOR
      -'D PF8=NEXT RECORD' TO COMMANDO.
      **************************************************************************
       GET-LAST-RECORD.
           MOVE HIGH-VALUES TO CA-RECORD-ID

           EXEC CICS STARTBR
                FILE('TLOG')
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(6)
                RESP(DATA-RESP-CODE)
           END-EXEC.

           EXEC CICS READPREV
                FILE('TLOG')
                INTO(HRD-TEST-TRACKING-RECORD)
                LENGTH(MASTER-RECORD-LRECL)
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(6)
                RESP(DATA-RESP-CODE)
           END-EXEC.

           EXEC CICS ENDBR
                FILE('TLOG')
           END-EXEC.

           IF DATA-RESP-CODE = ZEROS
               PERFORM DATA-FILE-TO-SCREEN
           ELSE
               PERFORM DATA-INITIALIZE-SCREEN-RECORD
               MOVE "RECORD NOT FOUND" TO MSGO
           END-IF.
           PERFORM SCREEN-IO-SEND-UPDATE.
      **************************************************************************
       GET-PREV-RECORD.
           EXEC CICS STARTBR
                FILE('TLOG')
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(6)
                RESP(DATA-RESP-CODE)
           END-EXEC.

           EXEC CICS READPREV
                FILE('TLOG')
                INTO(HRD-TEST-TRACKING-RECORD)
                LENGTH(MASTER-RECORD-LRECL)
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(6)
                RESP(DATA-RESP-CODE)
           END-EXEC.

           EXEC CICS READPREV
                FILE('TLOG')
                INTO(HRD-TEST-TRACKING-RECORD)
                LENGTH(MASTER-RECORD-LRECL)
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(6)
                RESP(DATA-RESP-CODE)
           END-EXEC.

           EXEC CICS ENDBR
                FILE('TLOG')
           END-EXEC.

           IF DATA-RESP-CODE = ZEROS
               PERFORM DATA-FILE-TO-SCREEN
           ELSE IF DATA-RESP-CODE = 20
               MOVE "NOTE: THIS IS THE FIRST RECORD..." TO MSGO
               PERFORM DATA-FILE-TO-SCREEN
           ELSE
               PERFORM DATA-INITIALIZE-SCREEN-RECORD
               MOVE "RECORD NOT FOUND" TO MSGO
           END-IF.
           PERFORM SCREEN-IO-SEND-UPDATE.
      **************************************************************************
       GET-NEXT-RECORD.
           EXEC CICS STARTBR
                FILE('TLOG')
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(6)
                RESP(DATA-RESP-CODE)
           END-EXEC.

           EXEC CICS READNEXT
                FILE('TLOG')
                INTO(HRD-TEST-TRACKING-RECORD)
                LENGTH(MASTER-RECORD-LRECL)
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(6)
                RESP(DATA-RESP-CODE)
           END-EXEC.

           EXEC CICS READNEXT
                FILE('TLOG')
                INTO(HRD-TEST-TRACKING-RECORD)
                LENGTH(MASTER-RECORD-LRECL)
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(6)
                RESP(DATA-RESP-CODE)
           END-EXEC.

           EXEC CICS ENDBR
                FILE('TLOG')
           END-EXEC.

           IF DATA-RESP-CODE = ZEROS
               PERFORM DATA-FILE-TO-SCREEN
           ELSE IF DATA-RESP-CODE = 20
               MOVE "NOTE: THIS IS THE LAST RECORD..." TO MSGO
               PERFORM DATA-FILE-TO-SCREEN
           ELSE
               PERFORM DATA-INITIALIZE-SCREEN-RECORD
               MOVE "RECORD NOT FOUND" TO MSGO
           END-IF.
           PERFORM SCREEN-IO-SEND-UPDATE.
      **************************************************************************
       TLOG-READ.
           EXEC CICS READ
                DATASET('TLOG')
                INTO(HRD-TEST-TRACKING-RECORD)
                LENGTH(MASTER-RECORD-LRECL)
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(6)
                RESP(DATA-RESP-CODE)
           END-EXEC.
      **************************************************************************
       TLOG-READ-UPDATE.
           EXEC CICS READ
                DATASET('TLOG')
                INTO(HRD-TEST-TRACKING-RECORD)
                LENGTH(MASTER-RECORD-LRECL)
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(6)
                UPDATE
                RESP(DATA-RESP-CODE)
           END-EXEC.
      **************************************************************************
       TLOG-REWRITE.
           EXEC CICS REWRITE
                DATASET('TLOG')
                FROM(HRD-TEST-TRACKING-RECORD)
                LENGTH(MASTER-RECORD-LRECL)
                RESP(DATA-RESP-CODE)
           END-EXEC.
      **************************************************************************
       CLEAR-MESSAGES.
           MOVE SPACES TO COMMANDO.
           MOVE SPACES TO MSGO.
      **************************************************************************
       DATA-FILE-TO-SCREEN.
           MOVE TEST-RECORD-ID TO CA-RECORD-ID.
           MOVE TEST-RECORD-ID TO IDO.
           MOVE TEST-CATEGORY TO CATEGORYO.
           MOVE TEST-GROUP TO GROUPO.
           MOVE TEST-AUTHOR TO AUTHORO.
           MOVE TEST-EMAIL TO EMAILO.
           MOVE TEST-YEAR TO YEARO.
           MOVE TEST-MONTH TO MONTHO.
           MOVE TEST-DAY TO DAYO.
           MOVE TEST-SUBJECT TO SUBJECTO.
           MOVE TEST-NOTE-1 TO NOTE1O.
           MOVE TEST-NOTE-2 TO NOTE2O.
           MOVE TEST-NOTE-3 TO NOTE3O.
           MOVE TEST-NOTE-4 TO NOTE4O.
           MOVE TEST-NOTE-5 TO NOTE5O.
           MOVE TEST-NOTE-6 TO NOTE6O.
           MOVE TEST-NOTE-7 TO NOTE7O.
           MOVE TEST-NOTE-8 TO NOTE8O.
           MOVE TEST-NOTE-9 TO NOTE9O.
           MOVE TEST-NOTE-10 TO NOTE10O.
           MOVE TEST-NOTE-11 TO NOTE11O.
           MOVE TEST-NOTE-12 TO NOTE12O.
           MOVE TEST-NOTE-13 TO NOTE13O.
           MOVE TEST-NOTE-14 TO NOTE14O.
           PERFORM BUILD-HEADER.
      **************************************************************************
       DATA-INITIALIZE-SCREEN-RECORD.
           MOVE LOW-VALUES TO TLOGO.
           PERFORM BUILD-HEADER.
      **************************************************************************
       DATA-SCREEN-TO-FILE.
           IF CATEGORYL > 0 THEN MOVE CATEGORYI TO TEST-CATEGORY
           END-IF.
           IF GROUPL > 0 THEN MOVE GROUPI TO TEST-GROUP
           END-IF.
           IF AUTHORL > 0 THEN MOVE AUTHORI TO TEST-AUTHOR
           END-IF.
           IF EMAILL > 0 THEN MOVE EMAILI TO TEST-EMAIL
           END-IF.
           IF YEARL > 0 THEN MOVE YEARI TO TEST-YEAR
           END-IF.
           IF MONTHL > 0 THEN MOVE MONTHI TO TEST-MONTH
           END-IF.
           IF DAYL > 0 THEN MOVE DAYI TO TEST-DAY
           END-IF.
           IF SUBJECTL > 0 THEN MOVE SUBJECTI TO TEST-SUBJECT
           END-IF.
           IF NOTE1L > 0 THEN MOVE NOTE1I TO TEST-NOTE-1
           END-IF.
           IF NOTE2L > 0 THEN MOVE NOTE2I TO TEST-NOTE-2
           END-IF.
           IF NOTE3L > 0 THEN MOVE NOTE3I TO TEST-NOTE-3
           END-IF.
           IF NOTE4L > 0 THEN MOVE NOTE4I TO TEST-NOTE-4
           END-IF.
           IF NOTE5L > 0 THEN MOVE NOTE5I TO TEST-NOTE-5
           END-IF.
           IF NOTE6L > 0 THEN MOVE NOTE6I TO TEST-NOTE-6
           END-IF.
           IF NOTE7L > 0 THEN MOVE NOTE7I TO TEST-NOTE-7
           END-IF.
           IF NOTE8L > 0 THEN MOVE NOTE8I TO TEST-NOTE-8
           END-IF.
           IF NOTE9L > 0 THEN MOVE NOTE9I TO TEST-NOTE-9
           END-IF.
           IF NOTE10L > 0 THEN MOVE NOTE10I TO TEST-NOTE-10
           END-IF.
           IF NOTE11L > 0 THEN MOVE NOTE11I TO TEST-NOTE-11
           END-IF.
           IF NOTE12L > 0 THEN MOVE NOTE12I TO TEST-NOTE-12
           END-IF.
           IF NOTE13L > 0 THEN MOVE NOTE13I TO TEST-NOTE-13
           END-IF.
           IF NOTE14L > 0 THEN MOVE NOTE14I TO TEST-NOTE-14
           END-IF.
      **************************************************************************
       END-OF-PROGRAM.
           EXEC CICS XCTL
                PROGRAM('HPMSCM02')
                RESP(XCTL-RESP-CODE)
           END-EXEC.
      **************************************************************************
       GENERAL-ERROR-ROUTINE.
           MOVE LOW-VALUES TO TLOGO.

           ADD EIBRESP TO ZERO GIVING X-EIB-RESP.
           ADD EIBRESP2 TO ZERO GIVING X-EIB-RESP2.

           IF X-EIB-RESP = 0000000012 THEN
               MOVE "FILE NOT FOUND" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000013 THEN
               MOVE "RECORD NOT FOUND" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000014 THEN
               MOVE "DUPLICATE RECORD" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000015 THEN
               MOVE "DUPLICATE KEY" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000016 THEN
               MOVE "INVALID RECORD" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000018 THEN
               MOVE "OUT OF SPACE" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000019 THEN
               MOVE "FILE NOT OPEN" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000022 THEN
               MOVE "LRECL ERROR" TO X-EIB-USER-MSG
           ELSE
               MOVE "GENERAL ERROR" TO X-EIB-USER-MSG
           END-IF.

           MOVE X-EIB-MESSAGE TO MSGO.
           PERFORM RECOVERY-ROUTINE.
      **************************************************************************
       GET-INFORMATION.
           PERFORM SCREEN-IO-GET-DATA-FOR-INQUIRY.
           PERFORM TLOG-READ.
           IF DATA-RESP-CODE = ZEROS
               PERFORM DATA-FILE-TO-SCREEN
           ELSE
               PERFORM DATA-INITIALIZE-SCREEN-RECORD
               MOVE "RECORD NOT FOUND" TO MSGO
           END-IF.
           PERFORM SCREEN-IO-SEND-UPDATE.
      **************************************************************************
       MAP-FAIL-ROUTINE.
           MOVE LOW-VALUES TO TLOGO.
           MOVE "MAP FAILURE" TO X-EIB-USER-MSG.
           ADD EIBRESP TO ZERO GIVING X-EIB-RESP.
           ADD EIBRESP2 TO ZERO GIVING X-EIB-RESP2.
           MOVE X-EIB-MESSAGE TO MSGO.
           PERFORM RECOVERY-ROUTINE.
      **************************************************************************
       RECOVERY-ROUTINE.
           MOVE -1 TO IDL.
           PERFORM SCREEN-IO-SEND-UPDATE.
      **************************************************************************
       SCREEN-IO-GET-DATA-FOR-INQUIRY.
           EXEC CICS RECEIVE
                MAP    ('TLOG')
                MAPSET ('HPMSB02')
                RESP(WS-RESPONSE)
                RESP2(WS-REASON-CODE)
           END-EXEC.

           IF IDL > ZERO
               MOVE IDI TO CA-RECORD-ID
           ELSE
               MOVE LOW-VALUES TO TLOGO
               MOVE "PLEASE ENTER 6 DIGIT ID NUMBER" TO MSGO
               PERFORM RECOVERY-ROUTINE
           END-IF.
      **************************************************************************
       SCREEN-IO-GET-DATA-FOR-UPDATE.
           EXEC CICS RECEIVE
                MAP    ('TLOG')
                MAPSET ('HPMSB02')
           END-EXEC.
      **************************************************************************
       SCREEN-IO-SEND-UPDATE.
           PERFORM BUILD-HEADER.
           EXEC CICS SEND
                MAP    ('TLOG')
                MAPSET ('HPMSB02')
                ERASE
           END-EXEC.
           EXEC CICS RETURN
                TRANSID  (LOCAL-TRAIN-ID)
                COMMAREA (WS-COMMAREA)
           END-EXEC.
      **************************************************************************
       TRANSFER-CONTROL.
           EXEC CICS XCTL
                PROGRAM('HPMSCM02')
                RESP(XCTL-RESP-CODE)
           END-EXEC.

           MOVE "PROGRAM NOT FOUND/ERROR" TO MSGO.
           MOVE -1 TO IDL.
           PERFORM RECOVERY-ROUTINE.
      **************************************************************************
       UPDATE-INFORMATION.
           PERFORM SCREEN-IO-GET-DATA-FOR-UPDATE.
           PERFORM UPDATE-INFORMATION-DATA.
      **************************************************************************
       UPDATE-INFORMATION-DATA.
           PERFORM TLOG-READ-UPDATE.
           IF DATA-RESP-CODE NOT = 0
               PERFORM GENERAL-ERROR-ROUTINE
           END-IF.
           PERFORM DATA-SCREEN-TO-FILE.
           PERFORM TLOG-REWRITE.
           IF DATA-RESP-CODE = 0
               MOVE LOW-VALUES TO TLOGO
               MOVE "UPDATE COMPLETED" TO MSGO
               PERFORM SCREEN-IO-SEND-UPDATE
           ELSE
               PERFORM GENERAL-ERROR-ROUTINE
           END-IF.
