      **************************************************************************
      * CATEGORY.: HPMS CICS PROGRAMS
      * GROUP....: COBOL
      * AUTHOR...: LANCE HAYNIE <LANCE@HAYNIEMAIL.COM>
      * DATE.....: 2017-10-02
      * PURPOSE..: HPMS MAIN MENU
      **************************************************************************
      * MODIFICATIONS
      * 2017-10-02 - LHAYNIE - INITIAL VERSION
      **************************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID.  HPMSCM01.
      **************************************************************************
       DATA DIVISION.
       WORKING-STORAGE SECTION.
       01  SWITCHES.
           05  VALID-DATA-SW                 PIC X.
       01  PROGRAM-TABLE.
           05  PROGRAM-LIST.
               10  PROGRAM-1                 PIC X(8) VALUE 'HPMSCM02'.
               10  PROGRAM-2                 PIC X(8) VALUE 'HPMSCM03'.
               10  PROGRAM-3                 PIC X(8) VALUE 'HPMSCM04'.
               10  PROGRAM-4                 PIC X(8) VALUE 'HPMSCM05'.
               10  PROGRAM-5                 PIC X(8) VALUE 'HPMSCM06'.
               10  PROGRAM-6                 PIC X(8) VALUE 'HPMSCM07'.
               10  PROGRAM-7                 PIC X(8) VALUE 'HPMSCM08'.
               10  PROGRAM-8                 PIC X(8) VALUE 'HPMSCM09'.
               10  PROGRAM-9                 PIC X(8) VALUE 'HPMSCM10'.
           05  PROGRAM-NAME REDEFINES PROGRAM-LIST
                 OCCURS 4 TIMES
                                             PIC X(8).
       01  SUBSCRIPTS.
           05  ACTION-SUB                    PIC 9.
       01  END-OF-SESSION-MESSAGE            PIC X(20)
             VALUE 'HPMS SESSION ENDED'.
       01  RESPONSE-CODE                     PIC S9(8) COMP.
       COPY HPMSB01.
       COPY DFHAID.
      **************************************************************************
       LINKAGE SECTION.
       01  DFHCOMMAREA                       PIC X.
      **************************************************************************
       PROCEDURE DIVISION.
           EVALUATE TRUE
               WHEN EIBCALEN = ZERO
                   MOVE LOW-VALUES TO MENUO
                   PERFORM SEND-MENU-MAP
               WHEN EIBAID = DFHCLEAR
                   MOVE LOW-VALUES TO MENUO
                   PERFORM SEND-MENU-MAP
               WHEN EIBAID = DFHPF12
                   PERFORM END-OF-PROGRAM
               WHEN EIBAID = DFHENTER
                   PERFORM PROCESS-MENU-MAP
               WHEN OTHER
                   MOVE LOW-VALUES TO MENUO
                   MOVE 'INVALID KEY PRESSED...' TO MSGO
                   PERFORM SEND-MENU-MAP
           END-EVALUATE.
           PERFORM END-OF-PROGRAM.
      **************************************************************************
       PROCESS-MENU-MAP.
           PERFORM RECEIVE-MENU-MAP.
           PERFORM EDIT-MENU-DATA.
           IF VALID-DATA-SW = 'Y'
               MOVE ACTIONI TO ACTION-SUB
               PERFORM BRANCH-TO-PROGRAM
           END-IF.
           PERFORM SEND-MENU-MAP.
      **************************************************************************
       PREPARE-MENU.
           MOVE "HAYNIE RESEARCH AND DEVELOPMENT" TO TITLE1O.
           MOVE "HPMSCM01" TO PNAMEO.
           MOVE "PROCESS MANAGEMENT SYSTEM" TO TITLE2O.
           MOVE "MAIN MENU" TO TITLE3O.
           MOVE "1  TEST LOG" TO OPTION1O.
           MOVE "TEST AND EXPERIMENT LOG" TO DESC1O.
           MOVE "2  PROPELLANT INV" TO OPTION2O.
           MOVE "PROPELLANT GRAIN INVENTORY" TO DESC2O.
           MOVE "3  INCIDENT" TO OPTION3O.
           MOVE "INCIDENT REPORTING" TO DESC3O.
           MOVE "4  INVENTORY" TO OPTION4O.
           MOVE "INVENTORY MANAGEMENT" TO DESC4O.
           MOVE "5  PROJECTS" TO OPTION5O.
           MOVE "PROJECT MANAGEMENT" TO DESC5O.
           MOVE "6  ACCOUNTING" TO OPTION6O.
           MOVE "ACCOUNTING & GENERAL LEDGER" TO DESC6O.
           MOVE "7  CRM" TO OPTION7O.
           MOVE "CUSTOMER DATABASE" TO DESC7O.
           MOVE "8  CMMS" TO OPTION8O.
           MOVE "WORK ORDERS" TO DESC8O.
           MOVE "9  NOTES" TO OPTION9O.
           MOVE "MISC NOTES" TO DESC9O.
           MOVE "PF KEYS: PF12=EXIT APPLICATION" TO COMMANDO.
      **************************************************************************
       RECEIVE-MENU-MAP.
           EXEC CICS
               RECEIVE MAP('MENU')
                       MAPSET('HPMSB01')
                       INTO(MENUI)
           END-EXEC.
      **************************************************************************
       EDIT-MENU-DATA.
           IF ACTIONI >= '0' AND <= '9'
               MOVE 'Y' TO VALID-DATA-SW
           ELSE
               MOVE 'YOU DID NOT ENTER A VALID OPTION' TO MSGO
               MOVE 'N' TO VALID-DATA-SW
           END-IF.
      **************************************************************************
       BRANCH-TO-PROGRAM.
           EXEC CICS
               XCTL PROGRAM (PROGRAM-NAME(ACTION-SUB))
               RESP(RESPONSE-CODE)
           END-EXEC.
           MOVE 'THAT OPTION IS NOT AVAILABLE' TO MSGO.
      **************************************************************************
       SEND-MENU-MAP.
           PERFORM PREPARE-MENU.
           EXEC CICS
                SEND MAP('MENU')
                 MAPSET('HPMSB01')
                 FROM(MENUO)
                 ERASE
           END-EXEC.
           MOVE LOW-VALUES TO MENUO.
           EXEC CICS RETURN
                TRANSID('HPMS')
                COMMAREA(MENUO)
                RESP(RESPONSE-CODE)
           END-EXEC.
      **************************************************************************
       END-OF-PROGRAM.
           EXEC CICS
               SEND TEXT FROM(END-OF-SESSION-MESSAGE)
                         ERASE
                         FREEKB
           END-EXEC.
           EXEC CICS
               RETURN
           END-EXEC.
