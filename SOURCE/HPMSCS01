      **************************************************************************
      * CATEGORY.: HPMS CICS PROGRAMS
      * GROUP....: COBOL
      * AUTHOR...: LANCE HAYNIE <LANCE@HAYNIEMAIL.COM>
      * DATE.....: 2017-10-09
      * PURPOSE..: ADD INCIDENT RECORD
      **************************************************************************
      * MODIFICATIONS
      * 2017-10-09 - LHAYNIE - INITIAL VERSION
      **************************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID.  HPMSCS01.
      **************************************************************************
       DATA DIVISION.
       WORKING-STORAGE SECTION.
       01  LOCAL-TRAIN-ID               PIC X(4) VALUE 'SF01'.
       01  MASTER-RECORD-LRECL          PIC S9(4) COMP VALUE 1214.
       01  DATA-RESP-CODE               PIC S9(8) COMP.
       01  XCTL-RESP-CODE               PIC S9(8) COMP.
       01  WS-RESPONSE                  PIC S9(8) COMP.
       01  WS-REASON-CODE               PIC S9(8) COMP.
       01  X-EIB-MESSAGE.
           05  FILLER                   PIC X(8) VALUE 'EIBRESP='.
           05  X-EIB-RESP               PIC 9(10) VALUE 0.
           05  FILLER                   PIC X(11) VALUE ', EIBRESP2='.
           05  X-EIB-RESP2              PIC 9(10) VALUE 0.
           05  FILLER                   PIC X VALUE SPACE.
           05  X-EIB-USER-MSG           PIC X(38) VALUE SPACES.
       01  WS-COMMAREA.
           05  CA-INCI-RECORD.
               10  CA-RECORD-ID         PIC X(6).
               10  CA-RECORD-ID-N REDEFINES CA-RECORD-ID PIC 9(6).
               10  FILLER               PIC X(1208).
       COPY HPMSB06.
       COPY INCIDENT.
       COPY DFHAID.
       COPY DFHBMSCA.
      **************************************************************************
       LINKAGE SECTION.
       01  DFHCOMMAREA.
           05  LS-INCI-RECORD.
               10  LS-RECORD-ID         PIC X(6).
               10  FILLER               PIC X(1208).
      **************************************************************************
       PROCEDURE DIVISION.
           IF EIBCALEN > ZERO
               MOVE DFHCOMMAREA TO WS-COMMAREA
           END-IF.

           PERFORM CLEAR-MESSAGES.

           EXEC CICS HANDLE CONDITION
                ERROR   (GENERAL-ERROR-ROUTINE)
                MAPFAIL (MAP-FAIL-ROUTINE)
                NOTFND  (GENERAL-ERROR-ROUTINE)
           END-EXEC.

           EVALUATE TRUE
             WHEN EIBCALEN = ZERO
                  PERFORM DATA-INITIALIZE-SCREEN-RECORD
                  PERFORM SCREEN-IO-SEND-UPDATE
             WHEN EIBAID = DFHCLEAR
                  PERFORM DATA-INITIALIZE-SCREEN-RECORD
                  PERFORM SCREEN-IO-SEND-UPDATE
             WHEN EIBAID = DFHPF5
                  PERFORM ADD-INFORMATION
             WHEN EIBAID = DFHPF3
                  PERFORM END-OF-PROGRAM
             WHEN OTHER
                  PERFORM DATA-INITIALIZE-SCREEN-RECORD
                  MOVE "YOU PRESSED AN INVALID KEY
      -         " TO MSGO
                  PERFORM SCREEN-IO-SEND-UPDATE
             END-EVALUATE.
             PERFORM END-OF-PROGRAM.
      **************************************************************************
       BUILD-HEADER.
           MOVE 'INCIDENT REPORTING' TO TITLE1O.
           MOVE 'ADD' TO TITLE2O.
           MOVE 'HPMSCS01' TO PNAMEO.
           MOVE 'PFKEYS: PF3=EXIT PF5=ADD RECORD' TO COMMANDO.
           MOVE DFHBMASK TO IDA.
      **************************************************************************
       CLEAR-MESSAGES.
           MOVE SPACES TO COMMANDO.
           MOVE SPACES TO MSGO.
      **************************************************************************
       MAP-FAIL-ROUTINE.
           MOVE LOW-VALUES TO INCIO.
           PERFORM DATA-INITIALIZE-SCREEN-RECORD.
           MOVE "MAP FAILURE" TO X-EIB-USER-MSG.
           ADD EIBRESP TO ZERO GIVING X-EIB-RESP.
           ADD EIBRESP2 TO ZERO GIVING X-EIB-RESP2.
           MOVE X-EIB-MESSAGE TO MSGO.
           PERFORM SCREEN-IO-SEND-UPDATE.
      **************************************************************************
       INCI-BUILD-ID.
           MOVE HIGH-VALUES TO CA-RECORD-ID

           EXEC CICS STARTBR
                FILE('INCIDENT')
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(6)
                RESP(DATA-RESP-CODE)
           END-EXEC.

           EXEC CICS READPREV
                FILE('INCIDENT')
                INTO(HRD-INCIDENT-RECORD)
                LENGTH(MASTER-RECORD-LRECL)
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(6)
                RESP(DATA-RESP-CODE)
           END-EXEC.

           ADD 1 TO CA-RECORD-ID-N.

           EXEC CICS ENDBR
                FILE('INCIDENT')
           END-EXEC.

           MOVE CA-RECORD-ID TO IDO.
      **************************************************************************
       INCI-WRITE.
           EXEC CICS WRITE
                DATASET('INCIDENT')
                FROM(HRD-INCIDENT-RECORD)
                LENGTH(MASTER-RECORD-LRECL)
                RIDFLD(CA-RECORD-ID)
                RESP(DATA-RESP-CODE)
           END-EXEC.
      **************************************************************************
       DATA-INITIALIZE-SCREEN-RECORD.
           MOVE LOW-VALUES TO INCIO.
           PERFORM INCI-BUILD-ID.
           PERFORM BUILD-HEADER.
      **************************************************************************
       SCREEN-IO-SEND-UPDATE.
           PERFORM BUILD-HEADER.
           EXEC CICS SEND
                MAP    ('INCI')
                MAPSET ('HPMSB06')
                ERASE
           END-EXEC.
           EXEC CICS RETURN
                TRANSID  (LOCAL-TRAIN-ID)
                COMMAREA (WS-COMMAREA)
           END-EXEC.
      **************************************************************************
       GENERAL-ERROR-ROUTINE.
           MOVE LOW-VALUES TO INCIO.

           ADD EIBRESP TO ZERO GIVING X-EIB-RESP.
           ADD EIBRESP2 TO ZERO GIVING X-EIB-RESP2.

           IF X-EIB-RESP = 0000000012 THEN
               MOVE "FILE NOT FOUND" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000013 THEN
               MOVE "RECORD NOT FOUND" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000014 THEN
               MOVE "DUPLICATE RECORD" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000015 THEN
               MOVE "DUPLICATE KEY" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000016 THEN
               MOVE "INVALID RECORD" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000018 THEN
               MOVE "OUT OF SPACE" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000019 THEN
               MOVE "FILE NOT OPEN" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000022 THEN
               MOVE "LRECL ERROR" TO X-EIB-USER-MSG
           ELSE
               MOVE "GENERAL ERROR" TO X-EIB-USER-MSG
           END-IF.

           MOVE -1 TO IDL.
           PERFORM DATA-INITIALIZE-SCREEN-RECORD.
           MOVE X-EIB-MESSAGE TO MSGO.
           PERFORM SCREEN-IO-SEND-UPDATE.
      **************************************************************************
       SCREEN-IO-GET-DATA-FOR-UPDATE.
           EXEC CICS RECEIVE
                MAP    ('INCI')
                MAPSET ('HPMSB06')
           END-EXEC.
      **************************************************************************
       DATA-SCREEN-TO-FILE.
           MOVE CA-RECORD-ID TO INCI-RECORD-ID.
           MOVE AUTHORI TO INCI-AUTHOR.
           MOVE EMAILI TO INCI-EMAIL.
           MOVE YEARI TO INCI-YEAR.
           MOVE MONTHI TO INCI-MONTH.
           MOVE DAYI TO INCI-DAY.
           MOVE SUBJECTI TO INCI-SUBJECT.
           MOVE NOTE1I TO INCI-NOTE-1.
           MOVE NOTE2I TO INCI-NOTE-2.
           MOVE NOTE3I TO INCI-NOTE-3.
           MOVE NOTE4I TO INCI-NOTE-4.
           MOVE NOTE5I TO INCI-NOTE-5.
           MOVE NOTE6I TO INCI-NOTE-6.
           MOVE NOTE7I TO INCI-NOTE-7.
           MOVE NOTE8I TO INCI-NOTE-8.
           MOVE NOTE9I TO INCI-NOTE-9.
           MOVE NOTE10I TO INCI-NOTE-10.
           MOVE NOTE11I TO INCI-NOTE-11.
           MOVE NOTE12I TO INCI-NOTE-12.
           MOVE NOTE13I TO INCI-NOTE-13.
           MOVE NOTE14I TO INCI-NOTE-14.
      **************************************************************************
       ADD-INFORMATION.
           PERFORM SCREEN-IO-GET-DATA-FOR-UPDATE.
           PERFORM ADD-INFORMATION-DATA.
      **************************************************************************
       ADD-INFORMATION-DATA.
           PERFORM DATA-SCREEN-TO-FILE.
           PERFORM INCI-WRITE.

           IF DATA-RESP-CODE = 0
               PERFORM DATA-INITIALIZE-SCREEN-RECORD
               MOVE "NEW INCIDENT ADDED" TO MSGO
               PERFORM SCREEN-IO-SEND-UPDATE
           ELSE
               PERFORM GENERAL-ERROR-ROUTINE
           END-IF.
      **************************************************************************
       END-OF-PROGRAM.
           EXEC CICS XCTL
                PROGRAM('HPMSCM04')
                RESP(XCTL-RESP-CODE)
           END-EXEC.
