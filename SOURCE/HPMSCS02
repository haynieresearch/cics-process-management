      **************************************************************************
      * CATEGORY.: HPMS CICS PROGRAMS
      * GROUP....: COBOL
      * AUTHOR...: LANCE HAYNIE <LANCE@HAYNIEMAIL.COM>
      * DATE.....: 2017-10-09
      * PURPOSE..: EDIT/VIEW INCIDENT RECORD
      **************************************************************************
      * MODIFICATIONS
      * 2017-10-09 - LHAYNIE - INITIAL VERSION
      * 2017-10-12 - LHAYNIE - ADDED START ON LAST RECORD
      * 2017-10-12 - LHAYNIE - ADDED NEXT/PREV RECORD FUNCTIONS
      **************************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID.  HPMSCS02.
      **************************************************************************
       DATA DIVISION.
       WORKING-STORAGE SECTION.
       01  LOCAL-TRAIN-ID               PIC X(4) VALUE 'SF02'.
       01  MASTER-RECORD-LRECL          PIC S9(4) COMP VALUE 1214.
       01  DATA-RESP-CODE               PIC S9(8) COMP.
       01  XCTL-RESP-CODE               PIC S9(8) COMP.
       01  WS-RESPONSE                  PIC S9(8) COMP.
       01  WS-REASON-CODE               PIC S9(8) COMP.
       01  X-EIB-MESSAGE.
           05  FILLER                   PIC X(8) VALUE 'EIBRESP='.
           05  X-EIB-RESP               PIC 9(10) VALUE 0.
           05  FILLER                   PIC X(11) VALUE ', EIBRESP2='.
           05  X-EIB-RESP2              PIC 9(10) VALUE 0.
           05  FILLER                   PIC X VALUE SPACE.
           05  X-EIB-USER-MSG           PIC X(38) VALUE SPACES.
       01  WS-COMMAREA.
           05  CA-INCI-RECORD.
               10  CA-RECORD-ID         PIC X(6).
               10  CA-RECORD-ID-N REDEFINES CA-RECORD-ID PIC 9(6).
               10  FILLER               PIC X(1208).
       COPY HPMSB06.
       COPY INCIDENT.
       COPY DFHAID.
       COPY DFHBMSCA.
      **************************************************************************
       LINKAGE SECTION.
       01  DFHCOMMAREA.
           05  LS-INCI-RECORD.
               10  LS-RECORD-ID         PIC X(6).
               10  FILLER               PIC X(1208).
      **************************************************************************
       PROCEDURE DIVISION.
           IF EIBCALEN > ZERO
               MOVE DFHCOMMAREA TO WS-COMMAREA
           END-IF.

           PERFORM CLEAR-MESSAGES.

           EXEC CICS HANDLE CONDITION
                ERROR   (GENERAL-ERROR-ROUTINE)
                MAPFAIL (MAP-FAIL-ROUTINE)
                NOTFND  (GENERAL-ERROR-ROUTINE)
           END-EXEC.

           EVALUATE TRUE
             WHEN EIBCALEN = ZERO
                  PERFORM DATA-INITIALIZE-SCREEN-RECORD
                  PERFORM GET-LAST-RECORD
             WHEN EIBAID = DFHCLEAR
                  PERFORM DATA-INITIALIZE-SCREEN-RECORD
                  PERFORM SCREEN-IO-SEND-UPDATE
             WHEN EIBAID = DFHPF3
                  PERFORM END-OF-PROGRAM
             WHEN EIBAID = DFHPF5
                  PERFORM UPDATE-INFORMATION
             WHEN EIBAID = DFHPF7
                  PERFORM DATA-INITIALIZE-SCREEN-RECORD
                  PERFORM GET-PREV-RECORD
             WHEN EIBAID = DFHPF8
                  PERFORM DATA-INITIALIZE-SCREEN-RECORD
                  PERFORM GET-NEXT-RECORD
             WHEN EIBAID = DFHENTER
                  PERFORM GET-INFORMATION
             WHEN OTHER
                  MOVE "YOU MUST ENTER A RECORD TO LOOKUP
      -         " TO MSGO
                  PERFORM RECOVERY-ROUTINE
             END-EVALUATE.
             PERFORM END-OF-PROGRAM.
      **************************************************************************
       BUILD-HEADER.
           MOVE 'INCIDENT REPORTING' TO TITLE1O.
           MOVE 'INQUIRY/UPDATE' TO TITLE2O.
           MOVE 'HPMSCS02' TO PNAMEO.
           MOVE 'ENTER=LOOKUP PFKEYS: PF3=EXIT PF5=UPDATE PF7=PREV RECOR
      -'D PF8=NEXT RECORD' TO COMMANDO.
      **************************************************************************
       GET-LAST-RECORD.
           MOVE HIGH-VALUES TO CA-RECORD-ID

           EXEC CICS STARTBR
                FILE('INCIDENT')
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(6)
                RESP(DATA-RESP-CODE)
           END-EXEC.

           EXEC CICS READPREV
                FILE('INCIDENT')
                INTO(HRD-INCIDENT-RECORD)
                LENGTH(MASTER-RECORD-LRECL)
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(6)
                RESP(DATA-RESP-CODE)
           END-EXEC.

           EXEC CICS ENDBR
                FILE('INCIDENT')
           END-EXEC.

           IF DATA-RESP-CODE = ZEROS
               PERFORM DATA-FILE-TO-SCREEN
           ELSE
               PERFORM DATA-INITIALIZE-SCREEN-RECORD
               MOVE "RECORD NOT FOUND" TO MSGO
           END-IF.
           PERFORM SCREEN-IO-SEND-UPDATE.
      **************************************************************************
       GET-PREV-RECORD.
           EXEC CICS STARTBR
                FILE('INCIDENT')
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(6)
                RESP(DATA-RESP-CODE)
           END-EXEC.

           EXEC CICS READPREV
                FILE('INCIDENT')
                INTO(HRD-INCIDENT-RECORD)
                LENGTH(MASTER-RECORD-LRECL)
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(6)
                RESP(DATA-RESP-CODE)
           END-EXEC.

           EXEC CICS READPREV
                FILE('INCIDENT')
                INTO(HRD-INCIDENT-RECORD)
                LENGTH(MASTER-RECORD-LRECL)
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(6)
                RESP(DATA-RESP-CODE)
           END-EXEC.

           EXEC CICS ENDBR
                FILE('INCIDENT')
           END-EXEC.

           IF DATA-RESP-CODE = ZEROS
               PERFORM DATA-FILE-TO-SCREEN
           ELSE IF DATA-RESP-CODE = 20
               MOVE "NOTE: THIS IS THE FIRST RECORD..." TO MSGO
               PERFORM DATA-FILE-TO-SCREEN
           ELSE
               PERFORM DATA-INITIALIZE-SCREEN-RECORD
               MOVE "RECORD NOT FOUND" TO MSGO
           END-IF.
           PERFORM SCREEN-IO-SEND-UPDATE.
      **************************************************************************
       GET-NEXT-RECORD.
           EXEC CICS STARTBR
                FILE('INCIDENT')
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(6)
                RESP(DATA-RESP-CODE)
           END-EXEC.

           EXEC CICS READNEXT
                FILE('INCIDENT')
                INTO(HRD-INCIDENT-RECORD)
                LENGTH(MASTER-RECORD-LRECL)
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(6)
                RESP(DATA-RESP-CODE)
           END-EXEC.

           EXEC CICS READNEXT
                FILE('INCIDENT')
                INTO(HRD-INCIDENT-RECORD)
                LENGTH(MASTER-RECORD-LRECL)
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(6)
                RESP(DATA-RESP-CODE)
           END-EXEC.

           EXEC CICS ENDBR
                FILE('INCIDENT')
           END-EXEC.

           IF DATA-RESP-CODE = ZEROS
               PERFORM DATA-FILE-TO-SCREEN
           ELSE IF DATA-RESP-CODE = 20
               MOVE "NOTE: THIS IS THE LAST RECORD..." TO MSGO
               PERFORM DATA-FILE-TO-SCREEN
           ELSE
               PERFORM DATA-INITIALIZE-SCREEN-RECORD
               MOVE "RECORD NOT FOUND" TO MSGO
           END-IF.
           PERFORM SCREEN-IO-SEND-UPDATE.
      **************************************************************************
       INCI-READ.
           EXEC CICS READ
                DATASET('INCIDENT')
                INTO(HRD-INCIDENT-RECORD)
                LENGTH(MASTER-RECORD-LRECL)
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(6)
                RESP(DATA-RESP-CODE)
           END-EXEC.
      **************************************************************************
       INCI-READ-UPDATE.
           EXEC CICS READ
                DATASET('INCIDENT')
                INTO(HRD-INCIDENT-RECORD)
                LENGTH(MASTER-RECORD-LRECL)
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(6)
                UPDATE
                RESP(DATA-RESP-CODE)
           END-EXEC.
      **************************************************************************
       INCI-REWRITE.
           EXEC CICS REWRITE
                DATASET('INCIDENT')
                FROM(HRD-INCIDENT-RECORD)
                LENGTH(MASTER-RECORD-LRECL)
                RESP(DATA-RESP-CODE)
           END-EXEC.
      **************************************************************************
       CLEAR-MESSAGES.
           MOVE SPACES TO COMMANDO.
           MOVE SPACES TO MSGO.
      **************************************************************************
       DATA-FILE-TO-SCREEN.
           MOVE INCI-RECORD-ID TO CA-RECORD-ID.
           MOVE INCI-RECORD-ID TO IDO.
           MOVE INCI-AUTHOR TO AUTHORO.
           MOVE INCI-EMAIL TO EMAILO.
           MOVE INCI-YEAR TO YEARO.
           MOVE INCI-MONTH TO MONTHO.
           MOVE INCI-DAY TO DAYO.
           MOVE INCI-LOCATION TO LOCATIONO.
           MOVE INCI-NOTE-1 TO NOTE1O.
           MOVE INCI-NOTE-2 TO NOTE2O.
           MOVE INCI-NOTE-3 TO NOTE3O.
           MOVE INCI-NOTE-4 TO NOTE4O.
           MOVE INCI-NOTE-5 TO NOTE5O.
           MOVE INCI-NOTE-6 TO NOTE6O.
           MOVE INCI-NOTE-7 TO NOTE7O.
           MOVE INCI-NOTE-8 TO NOTE8O.
           MOVE INCI-NOTE-9 TO NOTE9O.
           MOVE INCI-NOTE-10 TO NOTE10O.
           MOVE INCI-NOTE-11 TO NOTE11O.
           MOVE INCI-NOTE-12 TO NOTE12O.
           MOVE INCI-NOTE-13 TO NOTE13O.
           MOVE INCI-NOTE-14 TO NOTE14O.
           PERFORM BUILD-HEADER.
      **************************************************************************
       DATA-INITIALIZE-SCREEN-RECORD.
           MOVE LOW-VALUES TO INCIO.
           PERFORM BUILD-HEADER.
      **************************************************************************
       DATA-SCREEN-TO-FILE.
           IF AUTHORL > 0 THEN MOVE AUTHORI TO INCI-AUTHOR
           END-IF.
           IF EMAILL > 0 THEN MOVE EMAILI TO INCI-EMAIL
           END-IF.
           IF YEARL > 0 THEN MOVE YEARI TO INCI-YEAR
           END-IF.
           IF MONTHL > 0 THEN MOVE MONTHI TO INCI-MONTH
           END-IF.
           IF DAYL > 0 THEN MOVE DAYI TO INCI-DAY
           END-IF.
           IF LOCATIONL > 0 THEN MOVE LOCATIONI TO INCI-LOCATION
           END-IF.
           IF NOTE1L > 0 THEN MOVE NOTE1I TO INCI-NOTE-1
           END-IF.
           IF NOTE2L > 0 THEN MOVE NOTE2I TO INCI-NOTE-2
           END-IF.
           IF NOTE3L > 0 THEN MOVE NOTE3I TO INCI-NOTE-3
           END-IF.
           IF NOTE4L > 0 THEN MOVE NOTE4I TO INCI-NOTE-4
           END-IF.
           IF NOTE5L > 0 THEN MOVE NOTE5I TO INCI-NOTE-5
           END-IF.
           IF NOTE6L > 0 THEN MOVE NOTE6I TO INCI-NOTE-6
           END-IF.
           IF NOTE7L > 0 THEN MOVE NOTE7I TO INCI-NOTE-7
           END-IF.
           IF NOTE8L > 0 THEN MOVE NOTE8I TO INCI-NOTE-8
           END-IF.
           IF NOTE9L > 0 THEN MOVE NOTE9I TO INCI-NOTE-9
           END-IF.
           IF NOTE10L > 0 THEN MOVE NOTE10I TO INCI-NOTE-10
           END-IF.
           IF NOTE11L > 0 THEN MOVE NOTE11I TO INCI-NOTE-11
           END-IF.
           IF NOTE12L > 0 THEN MOVE NOTE12I TO INCI-NOTE-12
           END-IF.
           IF NOTE13L > 0 THEN MOVE NOTE13I TO INCI-NOTE-13
           END-IF.
           IF NOTE14L > 0 THEN MOVE NOTE14I TO INCI-NOTE-14
           END-IF.
      **************************************************************************
       END-OF-PROGRAM.
           EXEC CICS XCTL
                PROGRAM('HPMSCM02')
                RESP(XCTL-RESP-CODE)
           END-EXEC.
      **************************************************************************
       GENERAL-ERROR-ROUTINE.
           MOVE LOW-VALUES TO INCIO.

           ADD EIBRESP TO ZERO GIVING X-EIB-RESP.
           ADD EIBRESP2 TO ZERO GIVING X-EIB-RESP2.

           IF X-EIB-RESP = 0000000012 THEN
               MOVE "FILE NOT FOUND" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000013 THEN
               MOVE "RECORD NOT FOUND" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000014 THEN
               MOVE "DUPLICATE RECORD" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000015 THEN
               MOVE "DUPLICATE KEY" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000016 THEN
               MOVE "INVALID RECORD" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000018 THEN
               MOVE "OUT OF SPACE" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000019 THEN
               MOVE "FILE NOT OPEN" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000022 THEN
               MOVE "LRECL ERROR" TO X-EIB-USER-MSG
           ELSE
               MOVE "GENERAL ERROR" TO X-EIB-USER-MSG
           END-IF.

           MOVE X-EIB-MESSAGE TO MSGO.
           PERFORM RECOVERY-ROUTINE.
      **************************************************************************
       GET-INFORMATION.
           PERFORM SCREEN-IO-GET-DATA-FOR-INQUIRY.
           PERFORM INCI-READ.
           IF DATA-RESP-CODE = ZEROS
               PERFORM DATA-FILE-TO-SCREEN
           ELSE
               PERFORM DATA-INITIALIZE-SCREEN-RECORD
               MOVE "RECORD NOT FOUND" TO MSGO
           END-IF.
           PERFORM SCREEN-IO-SEND-UPDATE.
      **************************************************************************
       MAP-FAIL-ROUTINE.
           MOVE LOW-VALUES TO INCIO.
           MOVE "MAP FAILURE" TO X-EIB-USER-MSG.
           ADD EIBRESP TO ZERO GIVING X-EIB-RESP.
           ADD EIBRESP2 TO ZERO GIVING X-EIB-RESP2.
           MOVE X-EIB-MESSAGE TO MSGO.
           PERFORM RECOVERY-ROUTINE.
      **************************************************************************
       RECOVERY-ROUTINE.
           MOVE -1 TO IDL.
           PERFORM SCREEN-IO-SEND-UPDATE.
      **************************************************************************
       SCREEN-IO-GET-DATA-FOR-INQUIRY.
           EXEC CICS RECEIVE
                MAP    ('INCI')
                MAPSET ('HPMSB06')
                RESP(WS-RESPONSE)
                RESP2(WS-REASON-CODE)
           END-EXEC.

           IF IDL > ZERO
               MOVE IDI TO CA-RECORD-ID
           ELSE
               MOVE LOW-VALUES TO INCIO
               MOVE "PLEASE ENTER 6 DIGIT ID NUMBER" TO MSGO
               PERFORM RECOVERY-ROUTINE
           END-IF.
      **************************************************************************
       SCREEN-IO-GET-DATA-FOR-UPDATE.
           EXEC CICS RECEIVE
                MAP    ('INCI')
                MAPSET ('HPMSB06')
           END-EXEC.
      **************************************************************************
       SCREEN-IO-SEND-UPDATE.
           PERFORM BUILD-HEADER.
           EXEC CICS SEND
                MAP    ('INCI')
                MAPSET ('HPMSB06')
                ERASE
           END-EXEC.
           EXEC CICS RETURN
                TRANSID  (LOCAL-TRAIN-ID)
                COMMAREA (WS-COMMAREA)
           END-EXEC.
      **************************************************************************
       TRANSFER-CONTROL.
           EXEC CICS XCTL
                PROGRAM('HPMSCM04')
                RESP(XCTL-RESP-CODE)
           END-EXEC.

           MOVE "PROGRAM NOT FOUND/ERROR" TO MSGO.
           MOVE -1 TO IDL.
           PERFORM RECOVERY-ROUTINE.
      **************************************************************************
       UPDATE-INFORMATION.
           PERFORM SCREEN-IO-GET-DATA-FOR-UPDATE.
           PERFORM UPDATE-INFORMATION-DATA.
      **************************************************************************
       UPDATE-INFORMATION-DATA.
           PERFORM INCI-READ-UPDATE.
           IF DATA-RESP-CODE NOT = 0
               PERFORM GENERAL-ERROR-ROUTINE
           END-IF.
           PERFORM DATA-SCREEN-TO-FILE.
           PERFORM INCI-REWRITE.
           IF DATA-RESP-CODE = 0
               MOVE LOW-VALUES TO INCIO
               MOVE "UPDATE COMPLETED" TO MSGO
               PERFORM SCREEN-IO-SEND-UPDATE
           ELSE
               PERFORM GENERAL-ERROR-ROUTINE
           END-IF.
