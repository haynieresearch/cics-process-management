      **************************************************************************
      * CATEGORY.: HPMS CICS PROGRAMS
      * GROUP....: COBOL
      * AUTHOR...: LANCE HAYNIE <LANCE@HAYNIEMAIL.COM>
      * DATE.....: 2017-09-29
      * PURPOSE..: EDIT/VIEW PROPELLANT GRAIN
      **************************************************************************
      * MODIFICATIONS
      * 2017-09-29 - LHAYNIE - INITIAL VERSION
      **************************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID.  HPMSCP02.
      **************************************************************************
       DATA DIVISION.
       WORKING-STORAGE SECTION.
       01  LOCAL-TRAIN-ID               PIC X(4) VALUE 'PI02'.
       01  MASTER-RECORD-LRECL          PIC S9(4) COMP VALUE 449.
       01  DATA-RESP-CODE               PIC S9(8) COMP.
       01  XCTL-RESP-CODE               PIC S9(8) COMP.
       01  WS-RESPONSE                  PIC S9(8) COMP.
       01  WS-REASON-CODE               PIC S9(8) COMP.
       01  X-EIB-MESSAGE.
           05  FILLER                   PIC X(8) VALUE 'EIBRESP='.
           05  X-EIB-RESP               PIC 9(10) VALUE 0.
           05  FILLER                   PIC X(11) VALUE ', EIBRESP2='.
           05  X-EIB-RESP2              PIC 9(10) VALUE 0.
           05  FILLER                   PIC X VALUE SPACE.
           05  X-EIB-USER-MSG           PIC X(38) VALUE SPACES.
       01  WS-COMMAREA.
           05  CA-PINV-RECORD.
               10  CA-RECORD-ID         PIC X(12).
               10  FILLER               PIC X(437).
       COPY HPMSB03.
       COPY PROPINV.
       COPY DFHAID.
       COPY DFHBMSCA.
      **************************************************************************
       LINKAGE SECTION.
       01  DFHCOMMAREA.
           05  LS-PINV-RECORD.
               10  LS-RECORD-ID         PIC X(12).
               10  FILLER               PIC X(437).
      **************************************************************************
       PROCEDURE DIVISION.
           IF EIBCALEN > ZERO
               MOVE DFHCOMMAREA TO WS-COMMAREA
           END-IF.

           PERFORM CLEAR-MESSAGES.

           EXEC CICS HANDLE CONDITION
                ERROR   (GENERAL-ERROR-ROUTINE)
                MAPFAIL (MAP-FAIL-ROUTINE)
                NOTFND  (GENERAL-ERROR-ROUTINE)
           END-EXEC.

           EVALUATE TRUE
             WHEN EIBCALEN = ZERO
                  PERFORM DATA-INITIALIZE-SCREEN-RECORD
                  PERFORM SCREEN-IO-SEND-UPDATE
             WHEN EIBAID = DFHCLEAR
                  PERFORM DATA-INITIALIZE-SCREEN-RECORD
                  PERFORM SCREEN-IO-SEND-UPDATE
             WHEN EIBAID = DFHPF3
                  PERFORM END-OF-PROGRAM
             WHEN EIBAID = DFHPF5
                  PERFORM UPDATE-INFORMATION
             WHEN EIBAID = DFHENTER
                  PERFORM GET-INFORMATION
             WHEN OTHER
                  MOVE "YOU MUST ENTER A RECORD TO LOOKUP
      -         " TO MSGO
                  PERFORM RECOVERY-ROUTINE
             END-EVALUATE.
             PERFORM END-OF-PROGRAM.
      **************************************************************************
       BUILD-HEADER.
           MOVE 'PROPELLANT GRAIN INVENTORY' TO TITLE1O.
           MOVE 'INQUIRY/UPDATE' TO TITLE2O.
           MOVE 'HPMSCP02' TO PNAMEO.
           MOVE 'ENTER=LOOKUP PFKEYS: PF3=EXIT PF5=UPDATE' TO COMMANDO.
      **************************************************************************
       PINV-READ.
           EXEC CICS READ
                DATASET('PROPINV')
                INTO(HRD-GRAIN-INV-RECORD)
                LENGTH(MASTER-RECORD-LRECL)
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(12)
                RESP(DATA-RESP-CODE)
           END-EXEC.
      **************************************************************************
       PINV-READ-UPDATE.
           EXEC CICS READ
                DATASET('PROPINV')
                INTO(HRD-GRAIN-INV-RECORD)
                LENGTH(MASTER-RECORD-LRECL)
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(12)
                UPDATE
                RESP(DATA-RESP-CODE)
           END-EXEC.
      **************************************************************************
       PINV-REWRITE.
           EXEC CICS REWRITE
                DATASET('PROPINV')
                FROM(HRD-GRAIN-INV-RECORD)
                LENGTH(MASTER-RECORD-LRECL)
                RESP(DATA-RESP-CODE)
           END-EXEC.
      **************************************************************************
       CLEAR-MESSAGES.
           MOVE SPACES TO COMMANDO.
           MOVE SPACES TO MSGO.
      **************************************************************************
       DATA-FILE-TO-SCREEN.
           MOVE GRAIN-SERIAL TO CA-RECORD-ID.
           MOVE GRAIN-SERIAL TO SERIALO.
           MOVE GRAIN-TYPE TO TYPEO.
           MOVE GRAIN-FORMULA TO FORMULAO.
           MOVE GRAIN-STATUS TO STATUSO.
           MOVE GRAIN-QA TO QAO.
           MOVE GRAIN-QUALITY TO QUALITYO.
           MOVE GRAIN-DIAMETER TO DIAMETERO.
           MOVE GRAIN-LENGTH TO LENGTHO.
           MOVE GRAIN-WEIGHT TO WEIGHTO.
           MOVE GRAIN-NOTE1 TO NOTE1O.
           MOVE GRAIN-NOTE2 TO NOTE2O.
           MOVE GRAIN-NOTE3 TO NOTE3O.
           MOVE GRAIN-NOTE4 TO NOTE4O.
           MOVE GRAIN-NOTE5 TO NOTE5O.
           PERFORM BUILD-HEADER.
      **************************************************************************
       DATA-INITIALIZE-SCREEN-RECORD.
           MOVE LOW-VALUES TO PINVO.
           PERFORM BUILD-HEADER.
      **************************************************************************
       DATA-SCREEN-TO-FILE.
           IF TYPEL > 0 THEN MOVE TYPEI TO GRAIN-TYPE
           END-IF.
           IF FORMULAL > 0 THEN MOVE FORMULAI TO GRAIN-FORMULA
           END-IF.
           IF STATUSL > 0 THEN MOVE STATUSI TO GRAIN-STATUS
           END-IF.
           IF QAL > 0 THEN MOVE QAI TO GRAIN-QA
           END-IF.
           IF QUALITYL > 0 THEN MOVE QUALITYI TO GRAIN-QUALITY
           END-IF.
           IF DIAMETERL > 0 THEN MOVE DIAMETERI TO GRAIN-DIAMETER
           END-IF.
           IF LENGTHL > 0 THEN MOVE LENGTHI TO GRAIN-LENGTH
           END-IF.
           IF WEIGHTL > 0 THEN MOVE WEIGHTI TO GRAIN-WEIGHT
           END-IF.
           IF NOTE1L > 0 THEN MOVE NOTE1I TO GRAIN-NOTE1
           END-IF.
           IF NOTE2L > 0 THEN MOVE NOTE2I TO GRAIN-NOTE2
           END-IF.
           IF NOTE3L > 0 THEN MOVE NOTE3I TO GRAIN-NOTE3
           END-IF.
           IF NOTE4L > 0 THEN MOVE NOTE4I TO GRAIN-NOTE4
           END-IF.
           IF NOTE5L > 0 THEN MOVE NOTE5I TO GRAIN-NOTE5
           END-IF.
      **************************************************************************
       END-OF-PROGRAM.
           EXEC CICS XCTL
                PROGRAM('HPMSCM03')
                RESP(XCTL-RESP-CODE)
           END-EXEC.
      **************************************************************************
       GENERAL-ERROR-ROUTINE.
           MOVE LOW-VALUES TO PINVO.

           ADD EIBRESP TO ZERO GIVING X-EIB-RESP.
           ADD EIBRESP2 TO ZERO GIVING X-EIB-RESP2.

           IF X-EIB-RESP = 0000000012 THEN
               MOVE "FILE NOT FOUND" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000013 THEN
               MOVE "RECORD NOT FOUND" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000014 THEN
               MOVE "DUPLICATE RECORD" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000015 THEN
               MOVE "DUPLICATE KEY" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000016 THEN
               MOVE "INVALID RECORD" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000018 THEN
               MOVE "OUT OF SPACE" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000019 THEN
               MOVE "FILE NOT OPEN" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000022 THEN
               MOVE "LRECL ERROR" TO X-EIB-USER-MSG
           ELSE
               MOVE "GENERAL ERROR" TO X-EIB-USER-MSG
           END-IF.

           MOVE X-EIB-MESSAGE TO MSGO.
           PERFORM RECOVERY-ROUTINE.
      **************************************************************************
       GET-INFORMATION.
           PERFORM SCREEN-IO-GET-DATA-FOR-INQUIRY.
           PERFORM PINV-READ.
           IF DATA-RESP-CODE = ZEROS
               PERFORM DATA-FILE-TO-SCREEN
           ELSE
               PERFORM DATA-INITIALIZE-SCREEN-RECORD
               MOVE "RECORD NOT FOUND" TO MSGO
           END-IF.
           PERFORM SCREEN-IO-SEND-UPDATE.
      **************************************************************************
       MAP-FAIL-ROUTINE.
           MOVE LOW-VALUES TO PINVO.
           MOVE "MAP FAILURE" TO X-EIB-USER-MSG.
           ADD EIBRESP TO ZERO GIVING X-EIB-RESP.
           ADD EIBRESP2 TO ZERO GIVING X-EIB-RESP2.
           MOVE X-EIB-MESSAGE TO MSGO.
           PERFORM RECOVERY-ROUTINE.
      **************************************************************************
       RECOVERY-ROUTINE.
           MOVE -1 TO SERIALL.
           PERFORM SCREEN-IO-SEND-UPDATE.
      **************************************************************************
       SCREEN-IO-GET-DATA-FOR-INQUIRY.
           EXEC CICS RECEIVE
                MAP    ('PINV')
                MAPSET ('HPMSB03')
                RESP(WS-RESPONSE)
                RESP2(WS-REASON-CODE)
           END-EXEC.

           IF SERIALL > ZERO
               MOVE SERIALI TO CA-RECORD-ID
           ELSE
               MOVE LOW-VALUES TO PINVO
               MOVE "PLEASE ENTER 12 DIGIT SERIAL NUMBER" TO MSGO
               PERFORM RECOVERY-ROUTINE
           END-IF.
      **************************************************************************
       SCREEN-IO-GET-DATA-FOR-UPDATE.
           EXEC CICS RECEIVE
                MAP    ('PINV')
                MAPSET ('HPMSB03')
           END-EXEC.
      **************************************************************************
       SCREEN-IO-SEND-UPDATE.
           PERFORM BUILD-HEADER.
           EXEC CICS SEND
                MAP    ('PINV')
                MAPSET ('HPMSB03')
                ERASE
           END-EXEC.
           EXEC CICS RETURN
                TRANSID  (LOCAL-TRAIN-ID)
                COMMAREA (WS-COMMAREA)
           END-EXEC.
      **************************************************************************
       TRANSFER-CONTROL.
           EXEC CICS XCTL
                PROGRAM('HPMSCM03')
                RESP(XCTL-RESP-CODE)
           END-EXEC.

           MOVE "PROGRAM NOT FOUND/ERROR" TO MSGO.
           MOVE -1 TO SERIALL.
           PERFORM RECOVERY-ROUTINE.
      **************************************************************************
       UPDATE-INFORMATION.
           PERFORM SCREEN-IO-GET-DATA-FOR-UPDATE.
           PERFORM UPDATE-INFORMATION-DATA.
      **************************************************************************
       UPDATE-INFORMATION-DATA.
           PERFORM PINV-READ-UPDATE.
           IF DATA-RESP-CODE NOT = 0
               PERFORM GENERAL-ERROR-ROUTINE
           END-IF.
           PERFORM DATA-SCREEN-TO-FILE.
           PERFORM PINV-REWRITE.
           IF DATA-RESP-CODE = 0
               MOVE LOW-VALUES TO PINVO
               MOVE "UPDATE COMPLETED" TO MSGO
               PERFORM SCREEN-IO-SEND-UPDATE
           ELSE
               PERFORM GENERAL-ERROR-ROUTINE
           END-IF.
