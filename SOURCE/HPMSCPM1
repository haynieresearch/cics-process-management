      **************************************************************************
      * CATEGORY.: HPMS CICS PROGRAMS
      * GROUP....: COBOL
      * AUTHOR...: LANCE HAYNIE <LANCE@HAYNIEMAIL.COM>
      * DATE.....: 2017-10-09
      * PURPOSE..: ADD PROJECT RECORD
      **************************************************************************
      * MODIFICATIONS
      * 2017-10-09 - LHAYNIE - INITIAL VERSION
      **************************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID.  HPMSCPM1.
      **************************************************************************
       DATA DIVISION.
       WORKING-STORAGE SECTION.
       01  LOCAL-TRAIN-ID               PIC X(4) VALUE 'PM01'.
       01  MASTER-RECORD-LRECL          PIC S9(4) COMP VALUE 826.
       01  DATA-RESP-CODE               PIC S9(8) COMP.
       01  XCTL-RESP-CODE               PIC S9(8) COMP.
       01  WS-RESPONSE                  PIC S9(8) COMP.
       01  WS-REASON-CODE               PIC S9(8) COMP.
       01  X-EIB-MESSAGE.
           05  FILLER                   PIC X(8) VALUE 'EIBRESP='.
           05  X-EIB-RESP               PIC 9(10) VALUE 0.
           05  FILLER                   PIC X(11) VALUE ', EIBRESP2='.
           05  X-EIB-RESP2              PIC 9(10) VALUE 0.
           05  FILLER                   PIC X VALUE SPACE.
           05  X-EIB-USER-MSG           PIC X(38) VALUE SPACES.
       01  WS-COMMAREA.
           05  CA-PROJECT-RECORD.
               10  CA-RECORD-ID         PIC X(6).
               10  CA-RECORD-ID-N REDEFINES CA-RECORD-ID PIC 9(6).
               10  FILLER               PIC X(820).
       COPY HPMSB05.
       COPY PROJECTS.
       COPY DFHAID.
       COPY DFHBMSCA.
      **************************************************************************
       LINKAGE SECTION.
       01  DFHCOMMAREA.
           05  LS-PROJECT-RECORD.
               10  LS-RECORD-ID         PIC X(6).
               10  FILLER               PIC X(820).
      **************************************************************************
       PROCEDURE DIVISION.
           IF EIBCALEN > ZERO
               MOVE DFHCOMMAREA TO WS-COMMAREA
           END-IF.

           PERFORM CLEAR-MESSAGES.

           EXEC CICS HANDLE CONDITION
                ERROR   (GENERAL-ERROR-ROUTINE)
                MAPFAIL (MAP-FAIL-ROUTINE)
                NOTFND  (GENERAL-ERROR-ROUTINE)
           END-EXEC.

           EVALUATE TRUE
             WHEN EIBCALEN = ZERO
                  PERFORM DATA-INITIALIZE-SCREEN-RECORD
                  PERFORM SCREEN-IO-SEND-UPDATE
             WHEN EIBAID = DFHCLEAR
                  PERFORM DATA-INITIALIZE-SCREEN-RECORD
                  PERFORM SCREEN-IO-SEND-UPDATE
             WHEN EIBAID = DFHPF5
                  PERFORM ADD-INFORMATION
             WHEN EIBAID = DFHPF3
                  PERFORM END-OF-PROGRAM
             WHEN OTHER
                  PERFORM DATA-INITIALIZE-SCREEN-RECORD
                  MOVE "YOU PRESSED AN INVALID KEY
      -         " TO MSGO
                  PERFORM SCREEN-IO-SEND-UPDATE
             END-EVALUATE.
             PERFORM END-OF-PROGRAM.
      **************************************************************************
       BUILD-HEADER.
           MOVE 'PROJECT MANAGEMENT' TO TITLE1O.
           MOVE 'ADD' TO TITLE2O.
           MOVE 'HPMSCPM1' TO PNAMEO.
           MOVE 'PFKEYS: PF3=EXIT PF5=ADD RECORD' TO COMMANDO.
           MOVE DFHBMASK TO IDA.
      **************************************************************************
       CLEAR-MESSAGES.
           MOVE SPACES TO COMMANDO.
           MOVE SPACES TO MSGO.
      **************************************************************************
       MAP-FAIL-ROUTINE.
           MOVE LOW-VALUES TO PMGMTO.
           PERFORM DATA-INITIALIZE-SCREEN-RECORD.
           MOVE "MAP FAILURE" TO X-EIB-USER-MSG.
           ADD EIBRESP TO ZERO GIVING X-EIB-RESP.
           ADD EIBRESP2 TO ZERO GIVING X-EIB-RESP2.
           MOVE X-EIB-MESSAGE TO MSGO.
           PERFORM SCREEN-IO-SEND-UPDATE.
      **************************************************************************
       PROJECT-BUILD-ID.
           MOVE HIGH-VALUES TO CA-RECORD-ID

           EXEC CICS STARTBR
                FILE('PROJECTS')
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(6)
                RESP(DATA-RESP-CODE)
           END-EXEC.

           EXEC CICS READPREV
                FILE('PROJECTS')
                INTO(HRD-PROJECT-RECORD)
                LENGTH(MASTER-RECORD-LRECL)
                RIDFLD(CA-RECORD-ID)
                KEYLENGTH(6)
                RESP(DATA-RESP-CODE)
           END-EXEC.

           ADD 1 TO CA-RECORD-ID-N.

           EXEC CICS ENDBR
                FILE('PROJECTS')
           END-EXEC.

           MOVE CA-RECORD-ID TO IDO.
      **************************************************************************
       PROJECT-WRITE.
           EXEC CICS WRITE
                DATASET('PROJECTS')
                FROM(HRD-PROJECT-RECORD)
                LENGTH(MASTER-RECORD-LRECL)
                RIDFLD(CA-RECORD-ID)
                RESP(DATA-RESP-CODE)
           END-EXEC.
      **************************************************************************
       DATA-INITIALIZE-SCREEN-RECORD.
           MOVE LOW-VALUES TO PMGMTO.
           PERFORM PROJECT-BUILD-ID.
           PERFORM BUILD-HEADER.
      **************************************************************************
       SCREEN-IO-SEND-UPDATE.
           PERFORM BUILD-HEADER.
           EXEC CICS SEND
                MAP    ('PMGMT')
                MAPSET ('HPMSB05')
                ERASE
           END-EXEC.
           EXEC CICS RETURN
                TRANSID  (LOCAL-TRAIN-ID)
                COMMAREA (WS-COMMAREA)
           END-EXEC.
      **************************************************************************
       GENERAL-ERROR-ROUTINE.
           MOVE LOW-VALUES TO PMGMTO.

           ADD EIBRESP TO ZERO GIVING X-EIB-RESP.
           ADD EIBRESP2 TO ZERO GIVING X-EIB-RESP2.

           IF X-EIB-RESP = 0000000012 THEN
               MOVE "FILE NOT FOUND" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000013 THEN
               MOVE "RECORD NOT FOUND" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000014 THEN
               MOVE "DUPLICATE RECORD" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000015 THEN
               MOVE "DUPLICATE KEY" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000016 THEN
               MOVE "INVALID RECORD" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000018 THEN
               MOVE "OUT OF SPACE" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000019 THEN
               MOVE "FILE NOT OPEN" TO X-EIB-USER-MSG
           ELSE IF X-EIB-RESP = 0000000022 THEN
               MOVE "LRECL ERROR" TO X-EIB-USER-MSG
           ELSE
               MOVE "GENERAL ERROR" TO X-EIB-USER-MSG
           END-IF.

           MOVE -1 TO IDL.
           PERFORM DATA-INITIALIZE-SCREEN-RECORD.
           MOVE X-EIB-MESSAGE TO MSGO.
           PERFORM SCREEN-IO-SEND-UPDATE.
      **************************************************************************
       SCREEN-IO-GET-DATA-FOR-UPDATE.
           EXEC CICS RECEIVE
                MAP    ('PMGMT')
                MAPSET ('HPMSB05')
           END-EXEC.
      **************************************************************************
       DATA-SCREEN-TO-FILE.
           MOVE CA-RECORD-ID TO PROJECT-ID.
           MOVE NAMEI TO PROJECT-NAME.
           MOVE STATUSI TO PROJECT-STATUS.
           MOVE OPENI TO PROJECT-OPEN.
           MOVE PERCENTI TO PROJECT-PERCENT.
           MOVE SYEARI TO PROJECT-START-YEAR.
           MOVE SMONTHI TO PROJECT-START-MONTH.
           MOVE SDAYI TO PROJECT-START-DAY.
           MOVE EYEARI TO PROJECT-END-YEAR.
           MOVE EMONTHI TO PROJECT-END-MONTH.
           MOVE EDAYI TO PROJECT-END-DAY.
           MOVE STATUSNOTE1I TO PROJECT-STATUS-NOTE-1.
           MOVE STATUSNOTE2I TO PROJECT-STATUS-NOTE-2.
           MOVE STATUSNOTE3I TO PROJECT-STATUS-NOTE-3.
           MOVE STATUSNOTE4I TO PROJECT-STATUS-NOTE-4.
           MOVE STATUSNOTE5I TO PROJECT-STATUS-NOTE-5.
           MOVE NOTE1I TO PROJECT-NOTE-1.
           MOVE NOTE2I TO PROJECT-NOTE-2.
           MOVE NOTE3I TO PROJECT-NOTE-3.
           MOVE NOTE4I TO PROJECT-NOTE-4.
           MOVE NOTE5I TO PROJECT-NOTE-5.
      **************************************************************************
       ADD-INFORMATION.
           PERFORM SCREEN-IO-GET-DATA-FOR-UPDATE.
           PERFORM ADD-INFORMATION-DATA.
      **************************************************************************
       ADD-INFORMATION-DATA.
           PERFORM DATA-SCREEN-TO-FILE.
           PERFORM PROJECT-WRITE.

           IF DATA-RESP-CODE = 0
               PERFORM DATA-INITIALIZE-SCREEN-RECORD
               MOVE "NEW PROJECT ADDED" TO MSGO
               PERFORM SCREEN-IO-SEND-UPDATE
           ELSE
               PERFORM GENERAL-ERROR-ROUTINE
           END-IF.
      **************************************************************************
       END-OF-PROGRAM.
           EXEC CICS XCTL
                PROGRAM('HPMSCM06')
                RESP(XCTL-RESP-CODE)
           END-EXEC.
